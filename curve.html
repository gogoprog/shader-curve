<!-- Licensed under a BSD license. See license.html for license -->
<!DOCTYPE html>
<html>

<head>
  <meta charset="utf-8">
  <meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=yes">
  <title>Shader CatmullRom Curve</title>
  <link rel="stylesheet" href="style.css">
</head>

<body>
  <canvas id="c"></canvas>
</body>

</html>

<body>
  <script type="x-shader/x-fragment" id="curve-fs">#version 300 es
precision highp float;

in vec2 height;

out vec4 outColor;

void main() {
    float alpha = 1.0 - abs(height.x - 0.5) * 2.0;
    outColor = vec4(1.0, 1.0, 1.0, alpha);
}
</script>

  <script type="x-shader/x-vertex" id="curve-vs">#version 300 es
uniform int numVerts;
uniform vec2 resolution;
uniform float thickness;

out vec2 height;

const int NUM_POINTS = 10;

vec2 getPoint(int index) 
{
    vec2 p[NUM_POINTS];
    p[0] = vec2(-1.0, -1.0);
    p[1] = vec2(-0.9, 0.8);
    p[2] = vec2(0.0, 0.2);
    p[3] = vec2(0.6, 0.8);
    p[4] = vec2(0.8, 0.2);
    p[5] = vec2(-0.8, -0.5);
    p[6] = vec2(0.0, -0.8);
    p[7] = vec2(1.0, 0.2);
    p[8] = vec2(1.0, 0.0);
    p[9] = vec2(-1.0, -1.0);

    return p[index];
}

vec2 computePosition(float t) {
    int segment = int(t * float(NUM_POINTS - 3));
    t = t * float(NUM_POINTS - 3) - float(segment);

    // Calculate the Catmull-Rom spline for the segment
    vec2 a = getPoint(segment);
    vec2 b = getPoint(segment + 1);
    vec2 c = getPoint(segment + 2);
    vec2 d = getPoint(segment + 3);

    vec2 position = 0.5 * (
        (2.0 * b) +
        (-a + c) * t +
        (2.0 * a - 5.0 * b + 4.0 * c - d) * t * t +
        (-a + 3.0 * b - 3.0 * c + d) * t * t * t
    );

    return position;
}

vec2 computeTangent(float t) {
    int segment = int(t * float(NUM_POINTS - 3));
    t = t * float(NUM_POINTS - 3) - float(segment);

    // Calculate the Catmull-Rom spline for the segment
    vec2 a = getPoint(segment);
    vec2 b = getPoint(segment + 1);
    vec2 c = getPoint(segment + 2);
    vec2 d = getPoint(segment + 3);

    vec2 tangent = 0.5 * (
        (-a + c) +
        (4.0 * a - 10.0 * b + 8.0 * c - 2.0 * d) * t +
        (-3.0 * a + 9.0 * b - 9.0 * c + 3.0 * d) * t * t
    );

    return tangent;
}

void main() {
  int quads_count = numVerts / 6;
  int quad_index = gl_VertexID / 6;
  float t = float(quad_index) / float(quads_count);
  float t2 = float(quad_index+1) / float(quads_count);

  vec2 pos1 = computePosition(t);
  vec2 pos2 = computePosition(t2);
  vec2 tg1 = computeTangent(t);
  vec2 tg2 = computeTangent(t2);
  vec2 norm1 = normalize(vec2(-tg1.y, tg1.x));
  vec2 norm2 = normalize(vec2(-tg2.y, tg2.x));

  int index = gl_VertexID % 6;

  vec2 final_pos;

  vec2 hside1 = norm1 * (thickness);
  vec2 hside2 = norm2 * (thickness);

  vec2 quadCorner1 = pos1 + hside1;
  vec2 quadCorner2 = pos1 - hside1;
  vec2 quadCorner3 = pos2 - hside2;
  vec2 quadCorner4 = pos2 + hside2;

  switch(index) {
    case 0:
      final_pos = quadCorner1;
      height.x = 0.0;
      break;
    case 1:
      final_pos = quadCorner2;
      height.x = 1.0;
      break;
    case 2:
      final_pos = quadCorner4;
      height.x = 0.0;
      break;
    case 3:
      final_pos = quadCorner3;
      height.x = 1.0;
      break;
    case 4:
      final_pos = quadCorner2;
      height.x = 1.0;
      break;
    case 5:
      final_pos = quadCorner4;
      height.x = 0.0;
      break;

  }

  gl_Position = vec4(final_pos, 0, 1);
}
</script>

  <script src="https://cdnjs.cloudflare.com/ajax/libs/dat-gui/0.5/dat.gui.min.js" type="text/javascript"></script>
  <script src="https://webgl2fundamentals.org/webgl/resources/webgl-utils.js"></script>

  <script src="./ui.js"></script>
  <script src="./main.js"></script>


</body>
